package com.abc.rule.people.ipm

import com.abc.fuse.improve.attribute.FuseAttribute;
import com.abc.relation.RecordRelation;
import  com.zhsq.biz.constant.people.PeopleItem;
import  com.zhsq.biz.constant.family.FamilyItem;
import com.abc.rrc.record.Attribute;
import com.abc.rrc.factory.AttributeFactory;
import com.zhsq.biz.family.algorithm.DataIntrospection;
import com.zhsq.biz.constant.EnumKeyValue;
import com.zhsq.biz.constant.RelationType;
import com.abc.fuse.improve.attribute.leaf.FuseLeafAttribute;
import com.zhsq.biz.common.CommonCalculation;
import com.zhsq.biz.people.algorithm.IDIntrospection;
import com.zhsq.biz.constant.DateUtils;
import com.abc.fuse.improve.attribute.FuseLeafAttrFactory;
import com.zhsq.biz.common.OutputDisplay;
import com.zhsq.biz.common.AuthAlgorithm;


global java.lang.String hostCode; 
global java.lang.String hostType;

global java.lang.String userCode;
global java.lang.String recordCode;
global java.lang.String recordName;

global java.util.List attributeList;
global java.util.List addedLabelList;
global java.util.List removedLabelList;
global java.util.List addedLabelList;
global java.util.List putFuseLeafAttributeList;
global java.util.List addedLeafAttrList;
global java.util.Map removedLeafAttrMap;
global com.abc.rrc.record.RootRecord rootRecord;
global  com.abc.fuse.improve.ops.builder.RecordRelationOpsBuilder recordRelationOpsBuilder;
global com.abc.complexus.RecordComplexus recordComplexus;

global java.lang.String hjtime;//户籍更改：变动日期


rule "新增人口标记为没死亡"
	salience 9 
when
	FuseAttribute(name==PeopleItem.是否死亡, attribute.sqlValue == null) or (not FuseAttribute(name==PeopleItem.是否死亡))
then
	Attribute attr = AttributeFactory.newInstance(PeopleItem.是否死亡, EnumKeyValue.ENUM_是否_否);
	attributeList.add(attr);
end

rule "人口死亡标记为未确认"
	salience 9 
when
	FuseAttribute(name==PeopleItem.是否死亡,CommonCalculation.isBasicLawful(value), attribute.sqlValue==EnumKeyValue.ENUM_是否_是);
    FuseAttribute(name==PeopleItem.待确认死亡, attribute.sqlValue==null) or (not  FuseAttribute(name==PeopleItem.待确认死亡));
then 
	Attribute attrc = AttributeFactory.newInstance(PeopleItem.待确认死亡, EnumKeyValue.ENUM_待确认死亡_未确认);
	attributeList.add(attrc);
	Attribute attra = AttributeFactory.newInstance(PeopleItem.死亡人口对应任务, "1.停止资金发送");
	attributeList.add(attra);
end

rule "人口没死亡标记待确认为null"
	salience 9 
when
	$nameB:FuseAttribute(name==PeopleItem.是否死亡,CommonCalculation.isBasicLawful(value), attribute.sqlValue==EnumKeyValue.ENUM_是否_否);
then 
	Attribute namec = AttributeFactory.newInstance(PeopleItem.待确认死亡, null);
	attributeList.add(namec);

	Attribute named = AttributeFactory.newInstance(PeopleItem.死亡时间, null);
	attributeList.add(named);
	
	Attribute attra = AttributeFactory.newInstance(PeopleItem.死亡人口对应任务, null);
	attributeList.add(attra);
end

rule "获取最新户籍变更信息设置户籍地门牌号"
	salience 15
when
	$FAa:FuseLeafAttribute(name==PeopleItem.户籍变更_变动后街路巷,$leafCode:leafCode);
   $FAb:FuseLeafAttribute(name==PeopleItem.户籍变更_变动后门楼详址, $leafCode==leafCode);
    $FAc:FuseLeafAttribute(name==PeopleItem.户籍变更_更改户籍门牌号, $leafCode==leafCode, $value:attribute.sqlValue);
     $FAd:FuseLeafAttribute(name==PeopleItem.户籍变更_变动日期, $leafCode==leafCode);
	eval(String.valueOf(EnumKeyValue.ENUM_是否_是).equals(String.valueOf($value)))
	eval(hjtime==null || DateUtils.toLongTime(null, hjtime) < DateUtils.toLongTime(null, $FAd.getValueForShow()))
then    
	//设置户籍门牌号
	Attribute attribute=AttributeFactory.newInstance(PeopleItem.户籍地门牌号, $FAa.getValueForShow()+$FAb.getValueForShow());
	attributeList.add(attribute);
	
	FuseLeafAttribute leaf = FuseLeafAttrFactory.newInstance($FAc.getCode(), $FAc.getLeafName(), $FAc.getLeafCode(), PeopleItem.户籍变更_更改户籍门牌号, EnumKeyValue.ENUM_是否_否);
	putFuseLeafAttributeList.add(leaf);
	kcontext.getKnowledgeRuntime().setGlobal("hjtime", $FAd.getValueForShow());
end

rule "残疾信息有值设置【是否残疾】为是"
	salience 10 
when
	exists FuseLeafAttribute(name==PeopleItem.残疾信息_残疾类别)
	(not FuseAttribute( name==PeopleItem.是否残疾 )) or FuseAttribute( name==PeopleItem.是否残疾, attribute.sqlValue!=EnumKeyValue.ENUM_是否_是 )
then 
	Attribute attr = AttributeFactory.newInstance(PeopleItem.是否残疾, EnumKeyValue.ENUM_是否_是);
	attributeList.add(attr);
end

rule "把居住地址的值赋值给居住地门牌号"
	salience 10 
when
	$fa: FuseLeafAttribute(name==PeopleItem.居住信息_居住地址)
then 
	FuseLeafAttribute leafAttr = FuseLeafAttrFactory.newInstance($fa.getCode(), $fa.getLeafName(), $fa.getLeafCode(), PeopleItem.居住信息_居住地门牌号, $fa.getAttribute().getValueStr());
	putFuseLeafAttributeList.add(leafAttr);
end 
