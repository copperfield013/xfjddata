package com.abc.rule.worktask.ipm

import com.abc.fuse.improve.attribute.FuseAttribute;
import com.abc.relation.RecordRelation;
import  com.zhsq.biz.constant.people.PeopleItem;
import  com.zhsq.biz.constant.family.FamilyItem;
import com.abc.rrc.record.Attribute;
import com.abc.rrc.factory.AttributeFactory;
import com.zhsq.biz.family.algorithm.DataIntrospection;
import com.zhsq.biz.constant.EnumKeyValue;
import com.zhsq.biz.constant.RelationType;
import com.abc.fuse.improve.attribute.leaf.FuseLeafAttribute;
import com.zhsq.biz.common.CommonCalculation;
import com.zhsq.biz.people.algorithm.IDIntrospection;
import com.zhsq.biz.constant.DateUtils;
import com.abc.fuse.improve.attribute.FuseLeafAttrFactory;
import com.zhsq.biz.common.OutputDisplay;
import com.zhsq.biz.common.AuthAlgorithm;
import com.zhsq.biz.constant.worktask.WorkTaskItem;
import com.abc.sysdate.SystemDateTimeManager;
import com.zhsq.biz.common.AuthUtil;

global java.lang.String hostCode; 
global java.lang.String hostType;

global java.lang.String userCode;
global java.lang.String recordCode;
global java.lang.String recordName;

global java.util.List attributeList;
global java.util.List addedLabelList;
global java.util.List removedLabelList;
global java.util.List addedLabelList;
global java.util.List putFuseLeafAttributeList;
global java.util.List addedLeafAttrList;
global java.util.Map removedLeafAttrMap;
global com.abc.rrc.record.RootRecord rootRecord;
global  com.abc.ops.builder.RecordRelationOpsBuilder recordRelationOpsBuilder;
global com.abc.complexus.RecordComplexus recordComplexus;

rule "新建任务设置"
	salience 20 
when
	 FuseAttribute( name==WorkTaskItem.任务状态, sqlValue==EnumKeyValue.ENUM_任务状态_新建);
then 
	//超时任务为否
	 Attribute attribute=AttributeFactory.newInstance(WorkTaskItem.任务是否超时,EnumKeyValue.ENUM_是否_否);
	attributeList.add(attribute);
	//设置任务创建时间
	Attribute attributeTime=AttributeFactory.newInstance(WorkTaskItem.创建时间,SystemDateTimeManager.getCunrrentTime());
	attributeList.add(attributeTime);
	//设置任务创建人
	Attribute attrPeople=AttributeFactory.newInstance(WorkTaskItem.创建人,AuthUtil.getUserName(userCode));
	attributeList.add(attrPeople);
end

rule "归档任务"
	salience 9
when
    FuseAttribute( name==WorkTaskItem.归档废弃, sqlValue==EnumKeyValue.ENUM_归档废弃_归档);
	FuseAttribute( name==WorkTaskItem.任务状态, sqlValue!=EnumKeyValue.ENUM_任务状态_归档);
	$gdtime: FuseAttribute(name==WorkTaskItem.归档时间  );
then 
  	Attribute gd =  AttributeFactory.newInstance(WorkTaskItem.任务状态, EnumKeyValue.ENUM_任务状态_归档);
  	Attribute gdVg =  AttributeFactory.newInstance(WorkTaskItem.归档废弃, null);
  	Attribute gdtime =  AttributeFactory.newInstance(WorkTaskItem.归档时间, SystemDateTimeManager.getCunrrentTime());
	attributeList.add(gd);
	attributeList.add(gdVg);
	attributeList.add(gdtime);
end

rule "废弃任务"
	salience 9
when
    $gdVf:FuseAttribute( name==WorkTaskItem.归档废弃, sqlValue==EnumKeyValue.ENUM_归档废弃_废弃);
	$task:FuseAttribute( name==WorkTaskItem.任务状态, sqlValue!=EnumKeyValue.ENUM_任务状态_废弃);
then
	Attribute task =  AttributeFactory.newInstance(WorkTaskItem.任务状态, EnumKeyValue.ENUM_任务状态_废弃);
  	Attribute gdVf =  AttributeFactory.newInstance(WorkTaskItem.归档废弃, null);
  	
	attributeList.add(task);
	attributeList.add(gdVf);
end

rule "派发任务"
	salience 18
when
    $attr:FuseAttribute( name==WorkTaskItem.保存派发, sqlValue==EnumKeyValue.ENUM_保存派发_派发);
	$statu: FuseAttribute( name==WorkTaskItem.任务状态, sqlValue==EnumKeyValue.ENUM_任务状态_新建);
then
	Attribute statu=AttributeFactory.newInstance(WorkTaskItem.任务状态,EnumKeyValue.ENUM_任务状态_处理中);
	 Attribute attribute=AttributeFactory.newInstance(WorkTaskItem.派发时间,SystemDateTimeManager.getCunrrentTime());
	attributeList.add(statu);
	attributeList.add(attribute);
end

rule "任务有执行人设置任务领用为否"
	salience 10 
when
    RecordRelation( type==RelationType.RR_工作任务_任务执行人_用户);
    $nameFA: FuseAttribute( name==WorkTaskItem.是否需要领用, CommonCalculation.isBasicLawful(value), sqlValue==EnumKeyValue.ENUM_是否_是)
then 
	Attribute nameFA=AttributeFactory.newInstance(WorkTaskItem.是否需要领用,EnumKeyValue.ENUM_是否_否);
	attributeList.add(nameFA);
end


rule "认领任务设置任务执行人"
	salience 10 
when
	FuseAttribute(name==WorkTaskItem.是否认领任务,CommonCalculation.isBasicLawful(sqlValue),sqlValue==EnumKeyValue.ENUM_是否_是);
    not RecordRelation( type==RelationType.RR_工作任务_任务执行人_用户);
then 
	recordRelationOpsBuilder.putRelation(RelationType.RR_工作任务_任务执行人_用户,userCode);
	
	Attribute nameFA=AttributeFactory.newInstance(WorkTaskItem.是否需要领用,EnumKeyValue.ENUM_是否_否);
	attributeList.add(nameFA);
end
